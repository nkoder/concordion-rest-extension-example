apply plugin: 'java'
apply plugin: 'org.ajoberstar.github-pages'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.ajoberstar:gradle-git:1.5.1'
    }
}
ext {
    springBootVersion = '1.4.0.RELEASE'
}

repositories {
    mavenCentral()
}

sourceSets {
    test {
        resources.srcDir file('src/test/java')
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {

    compile "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
    compile 'commons-io:commons-io:2.4'

    compile 'com.itextpdf:itextpdf:5.5.9'
    compile 'com.itextpdf.tool:xmlworker:5.5.9'

    testCompile "org.springframework.boot:spring-boot-starter-test:$springBootVersion"

    testCompile 'junit:junit:4.12'
    testCompile "org.concordion:concordion:2.0.2"
    testCompile "pl.pragmatists:concordion-rest-extension:0.5.4"
}

test {
    group 'Concordion tests'
    description = "Run Concordion"
    systemProperty 'concordion.output.dir', "$reporting.baseDir/concordion"
    ignoreFailures = true
    testLogging.showStandardStreams = true
    outputs.upToDateWhen { false }
}

githubPages {
    repoUri = 'git@github.com:nkoder/concordion-rest-extension-example.git'
    pages {
        from "$reporting.baseDir/concordion"
    }
}

task createIndex(type: Copy) {
    from('src/test/resources') {
        include 'index.html'
    }
    into "$reporting.baseDir/concordion"
}

test.dependsOn createIndex
publishGhPages.dependsOn test

