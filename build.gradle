apply plugin: 'java'
apply plugin: 'org.ajoberstar.github-pages'

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'org.ajoberstar:gradle-git:1.1.0'
    }
}
ext {
    springBootVersion = '1.1.9.RELEASE'
}

repositories {
    mavenCentral()
    mavenLocal()
}

sourceSets {
    test {
        resources.srcDir file('src/test/java')
    }
}

sourceCompatibility = 1.8

dependencies {

    compile "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
    compile 'commons-io:commons-io:2.4'

    compile 'com.itextpdf:itextpdf:5.4.2'
    compile 'com.itextpdf.tool:xmlworker:5.4.1'

    testCompile "org.springframework.boot:spring-boot-starter-test:$springBootVersion"

    testCompile 'junit:junit:4.11'
    testCompile "org.concordion:concordion:1.4.6"
    testCompile "pl.pragmatists:concordion-rest-extension:0.5.1"
}

test {
    group 'Concordion tests'
    description = "Run Concordion"
    systemProperty 'concordion.output.dir', "$reporting.baseDir/concordion"
    ignoreFailures = true
}

githubPages {
    repoUri = 'git@github.com:nkoder/concordion-rest-extension-example.git'
    pages {
        from "$reporting.baseDir/concordion"
    }
}

task createIndex(type: Copy) {
    from('src/test/resources') {
        include 'index.html'
    }
    into "$reporting.baseDir/concordion"
}

test.dependsOn createIndex
publishGhPages.dependsOn test

